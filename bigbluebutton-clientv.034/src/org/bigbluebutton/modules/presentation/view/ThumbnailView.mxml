<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" 
    width="100%" height="100%" 
    horizontalAlign="center" paddingBottom="5" 
    creationComplete="init()" verticalScrollPolicy="off" horizontalScrollPolicy="off" xmlns:panzoom="com.adobe.wheelerstreet.fig.panzoom.*" xmlns:local="*">
    
	<mx:Script>
		<![CDATA[
			import org.bigbluebutton.modules.presentation.model.vo.SlidesDeck;
			import org.bigbluebutton.modules.presentation.model.vo.Slide;
			import org.bigbluebutton.modules.presentation.PresentationFacade;
		import mx.collections.ArrayCollection;
		import mx.binding.utils.BindingUtils;
		import flash.events.Event;
		import mx.events.ListEvent;
		import mx.events.FlexEvent;

		private var startDragMouseX:Number;
		private var startDragMouseY:Number;
		private var startDragX:Number;
		private var startDragY:Number;
		
		[Bindable]	
		public var model:PresentationFacade = PresentationFacade.getInstance();
		
		[Bindable]
		private var slideWord:String = "Slides";
				
		[Bindable]
        public var deck:SlidesDeck;
        		
		[Bindable]
		public var selectedSlide:Slide = new Slide();
				
		private function init() : void
		{
			BindingUtils.bindSetter(handleChangedSlides, deck, "slides");
			addEventListener(MouseEvent.MOUSE_WHEEL, zoom);
			myLoader.addEventListener(MouseEvent.MOUSE_DOWN, mouseDown);
			myLoader.addEventListener(MouseEvent.MOUSE_UP, mouseUp);
			myLoader.addEventListener(MouseEvent.MOUSE_OUT, mouseOut);
		}	
		
		private function handleChangedSlides(slides : ArrayCollection) : void
		{
			thumbsBox.invalidateDisplayList();
			slideList.invalidateDisplayList();
			thumbsBox.validateNow();
			slideList.validateNow();
		}

		public function updateScroll () : void 
		{
			thumbsBox.verticalScrollPosition = thumbsBox.maxVerticalScrollPosition;
		}
		
		private function zoom(e:MouseEvent):void{
			if (!model.presentation.isPresenter) return;
			
			var wid:Number = myLoader.width + e.delta * 10;
			var hei:Number = myLoader.height + e.delta *10;
			
			if (hei >= this.height || hei >= myLoader.height){
				myLoader.width += e.delta * 10;
				myLoader.height += e.delta * 10;
			} 
			
			dispatchEvent(new Event(ThumbnailViewMediator.ZOOM));
		}
		
		private function mouseDown(e:MouseEvent):void{
			if (!model.presentation.isPresenter) return;
			
			startDragX = myLoader.x;
			startDragY = myLoader.y;
			startDragMouseX = imageCanvas.mouseX;
			startDragMouseY = imageCanvas.mouseY;
			addEventListener(MouseEvent.MOUSE_MOVE, mouseMove);
		}
		
		private function mouseMove(e:MouseEvent):void{
			if (!model.presentation.isPresenter) return;
			
			myLoader.x = startDragX + (imageCanvas.mouseX - startDragMouseX);
			myLoader.y = startDragY + (imageCanvas.mouseY - startDragMouseY);
			dispatchEvent(new Event(ThumbnailViewMediator.MOVE));
		}
		
		private function mouseUp(e:MouseEvent):void{
			if (!model.presentation.isPresenter) return;
			
			removeEventListener(MouseEvent.MOUSE_MOVE, mouseMove);
		}
		
		private function mouseOut(e:MouseEvent):void{
			if (!model.presentation.isPresenter) return;
			
			removeEventListener(MouseEvent.MOUSE_MOVE, mouseMove);
		}
		]]>
	</mx:Script>

	<mx:Binding source="model.presentation.decks as SlidesDeck" destination="deck" />
	<mx:Binding source="deck.selected" destination="slideList.selectedIndex" />	
	<mx:Binding source="slideList.selectedIndex" destination="deck.selected" />
	<mx:Binding source="deck.slides.getItemAt(deck.selected) as Slide" destination="selectedSlide" />
	
	<mx:Canvas id="imageCanvas" width="100%" height="100%" verticalScrollPolicy="off" horizontalScrollPolicy="off">
		<mx:Image id="myLoader" width="100%" height="100%"
    		scaleContent="true" maintainAspectRatio="true" showBusyCursor="true"
    		completeEffect="Fade"
        	source="{selectedSlide.source}" />
	</mx:Canvas>    	
        
        <!--<local:ZoomViewer id="myLoader" width="100%" height="100%" content="{selectedSlide.source}" /> -->


		
	<mx:Spacer height="15" />
	
	<mx:VBox id="thumbsBox" width="100" height="100%" styleName="thumbnailListBorderBox"
	    verticalGap="2" horizontalAlign="center" paddingBottom="5" updateComplete="updateScroll()"
	    borderStyle="solid" cornerRadius="10">			
		<mx:List id="slideList" dataProvider="{deck.slides}" paddingBottom="0"
		    width="100%" height="100%" selectedIndex="0" change="dispatchEvent(new Event(ThumbnailViewMediator.SEND_PAGE_NUM))"
		    itemRenderer="org.bigbluebutton.modules.presentation.view.Thumbnail"
		    styleName="thumbnailList" editorUsesEnterKey="true"/>   
	</mx:VBox>

</mx:HBox>
