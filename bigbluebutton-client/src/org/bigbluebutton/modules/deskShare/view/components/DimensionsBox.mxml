<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton - http://www.bigbluebutton.org
  
  Copyright (c) 2008-2009 by respective authors (see below). All rights reserved.
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 3 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, If not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
--> 

<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="onCreationComplete()" 
	backgroundColor="gray" toolTip="Drag the black box to the area of the screen you wish to share" 
	xmlns:components="org.bigbluebutton.modules.deskShare.view.components.*">
    
	<mx:Script>
		<![CDATA[
			import net.digitalprimates.fluint.events.AsyncEvent;
			import mx.core.UIComponent;

			public static const SCALE:Number = 5;
			private var screenWidth:Number;
			private var screenHeight:Number;
			
			private var startDragX:Number;
			private var startDragY:Number;
			private var startDragMouseX:Number;
			private var startDragMouseY:Number;
			
			public var videoHolder:UIComponent;
			private var videoPlayer:Video;
			private var ns:NetStream;
			
			[Bindable]
			private var suggestedFrameRate:Number;
			[Bindable]
			private var suggestedQuality:String;
			
			private function onCreationComplete():void{
				screenWidth = Capabilities.screenResolutionX;
				screenHeight = Capabilities.screenResolutionY;
				this.width = Math.round(screenWidth / SCALE);
				this.height = Math.round(screenHeight / SCALE);
				updateBoxInfo();
			}
			
			public function startThumbnail(nc:NetConnection, streamName:String):void{
				LogUtil.debug("DimensionsBox - Starting thumbnail view");
				LogUtil.debug("connection:" + nc.uri + " stream:" + streamName);
				this.visible = true;
				box.visible = false;
				
				videoHolder = new UIComponent();
				videoPlayer = new Video(box.width, box.height);
				videoPlayer.width = box.width;
				videoPlayer.height = box.height;
				videoHolder.width = box.width;
				videoHolder.height = box.height;
				videoHolder.setActualSize(box.width, box.height);
				videoHolder.addChild(videoPlayer);
				this.addChild(videoHolder);
				videoHolder.x = box.x;
				videoHolder.y = box.y;
				
				videoHolder.addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
				videoHolder.addEventListener(MouseEvent.MOUSE_UP, onMouseUp);
				this.addEventListener(MouseEvent.MOUSE_OUT, onMouseOut);
				
				ns = new NetStream(nc);
				ns.addEventListener(AsyncErrorEvent.ASYNC_ERROR, onAsyncError);
				ns.client = this;
				ns.bufferTime = 0;
				ns.receiveVideo(true);
				ns.receiveAudio(false);
				videoPlayer.attachNetStream(ns);
				ns.play(streamName);
			}
			
			public function stopThumbnail():void{
				this.removeChild(videoHolder);
				videoPlayer.clear();
				videoPlayer = null;
				videoHolder = null;
				this.removeEventListener(MouseEvent.MOUSE_OUT, onMouseOut);
				box.visible = true;
			}
			
			private function onMouseDown(e:MouseEvent):void{
				startDragX = videoHolder.x;
				startDragY = videoHolder.y;
				startDragMouseX = this.mouseX;
				startDragMouseY = this.mouseY;
				videoHolder.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			}
			
			private function onMouseMove(e:MouseEvent):void{
				var newX:Number = startDragX + (this.mouseX - startDragMouseX);
				var newY:Number = startDragY + (this.mouseY - startDragMouseY);
				
				if (newX < 0) videoHolder.x = 0;
				else if (newX > this.width - (box.width+5)) videoHolder.x = this.width - box.width;
				else videoHolder.x = newX;
				
				if (newY < 0) videoHolder.y = 0;
				else if (newY > this.height - (box.height+5)) videoHolder.y = this.height - box.height;
				else videoHolder.y = newY;
				
				ExternalInterface.call("setScreenCoordinates", getXPosition(), getYPosition());
			}
			
			private function onMouseUp(e:MouseEvent):void{
				videoHolder.removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			}
			
			private function onMouseOut(e:MouseEvent):void{
				videoHolder.removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			}
			
			private function getXPosition():Number{
				return videoHolder.x * SCALE;
			}
			
			private function getYPosition():Number{
				return videoHolder.y * SCALE;
			}
			
			private function onAsyncError(e:AsyncErrorEvent):void{
				
			}
			
			private function updateBoxInfo():void{
				if (box.width *SCALE >= Capabilities.screenResolutionX) lblWidth.text 
						= Capabilities.screenResolutionX.toString();
				if (box.height*SCALE >= Capabilities.screenResolutionY) lblHeight.text
						= Capabilities.screenResolutionY.toString();
				var area:Number = (box.width*SCALE) * (box.height*SCALE);
				if (area > 1000000){
					 suggestedFrameRate = 1;
					 suggestedQuality = "Poor";
				} else if (area > 600000){
					 suggestedFrameRate = 2;
					 suggestedQuality = "Fair"
				} else if (area > 300000){
					 suggestedFrameRate = 4;
					 suggestedQuality = "Good"
				} else if (area > 150000){
					 suggestedFrameRate = 8;
					 suggestedQuality = "Excellent";
				} else{
		 			 suggestedFrameRate = 10;
		 			 suggestedQuality = "Excellent";
		 		}
			}

		]]>
	</mx:Script>

	<components:ResizableBox id="box" x="10" y="10" resize="updateBoxInfo()" useHandCursor="true"
		width="{(Math.round((Capabilities.screenResolutionX/2)/SCALE))}" 
		height="{(Math.round((Capabilities.screenResolutionY - 100)/SCALE))}" 
		minWidth="{Math.round(300/SCALE)}" 
		minHeight="{Math.round(300/SCALE)}" 
		maxWidth="{Math.round((Capabilities.screenResolutionX)/SCALE)}" 
		maxHeight="{Math.round((Capabilities.screenResolutionY)/SCALE)}" 
		verticalScrollPolicy="off" horizontalScrollPolicy="off">
        <mx:HBox horizontalGap="0">
        	<mx:Label id="lblWidth" text="{(box.width * SCALE).toString()}"/>
        	<mx:Label text="x"/>
        	<mx:Label id="lblHeight" text="{(box.height * SCALE).toString()}"/>
        </mx:HBox>
        <mx:HBox>
        	<mx:Label text="FrameRate: " />
        	<mx:Label text="{suggestedFrameRate}" />
        </mx:HBox>
        <mx:HBox>
        	<mx:Label text="Quality: " />
        	<mx:Label text="{suggestedQuality}" />
        </mx:HBox>
	</components:ResizableBox>
</mx:Canvas>
