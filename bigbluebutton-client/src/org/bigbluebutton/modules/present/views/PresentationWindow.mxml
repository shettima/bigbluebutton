<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton - http://www.bigbluebutton.org
  
  Copyright (c) 2008-2009 by respective authors (see below). All rights reserved.
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 3 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, If not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
--> 


<pres:MDIWindow xmlns:mx="http://www.adobe.com/2006/mxml"  
	xmlns:thumb="org.bigbluebutton.modules.present.views.*"
	xmlns:pres="flexlib.mdi.containers.*"
	paddingBottom="0" paddingTop="0" 
	paddingLeft="0" paddingRight="0" 
	showCloseButton="false"
    verticalScrollPolicy="off" horizontalScrollPolicy="off"
	implements="org.bigbluebutton.common.IBbbModuleWindow"
	mouseMove="hideThumbnails()"
	creationComplete="onCreationComplete()"
	rollOut="hideThumbs()" xmlns:mate="http://mate.asfusion.com/"
	width="600" height="450" x="220" y="0"
	title="{currentPresentation}" keyUp="onKeyUp(event)">
	
	<mate:Dispatcher id="globalDispatcher" />
	<mate:Listener type="{MadePresenterEvent.SWITCH_TO_PRESENTER_MODE}" method="becomePresenter" />
	<mate:Listener type="{MadePresenterEvent.SWITCH_TO_VIEWER_MODE}" method="becomeViewer" />
	<mate:Listener type="{PresentationEvent.PRESENTATION_LOADED}" method="handlePresentationLoadedEvent" />
	<mate:Listener type="{NavigationEvent.GOTO_PAGE}" method="gotoPage" />
	<mate:Listener type="{UploadEvent.CLEAR_PRESENTATION}" method="clearPresentation" />
	<mate:Listener type="{SlideResizedEvent.SLIDE_RESIZED_EVENT}" method="handleSlideResizedEvent" />
	<mate:Listener type="{DisplaySlideEvent.DISPLAY_SLIDE_EVENT}" method="handleDisplaySlideEvent" />
	
	<mx:Script>
		<![CDATA[
			import org.bigbluebutton.modules.present.events.SlideResizedEvent;
			import org.bigbluebutton.modules.present.events.WindowResizedEvent;
			import org.bigbluebutton.modules.present.events.DisplaySlideEvent;
			import org.bigbluebutton.main.events.MadePresenterEvent;
			import org.bigbluebutton.modules.present.events.SlideEvent;
			import mx.controls.Alert;
			import org.bigbluebutton.modules.present.managers.Slide;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			import mx.effects.Move;
			import org.bigbluebutton.modules.present.events.MoveEvent;
			import org.bigbluebutton.modules.present.events.ZoomEvent;
			import org.bigbluebutton.modules.present.managers.SlideManager;
			import org.bigbluebutton.modules.present.events.PresentationEvent;
			import org.bigbluebutton.modules.present.events.PresenterCommands;
			import org.bigbluebutton.modules.present.events.UploadEvent;
			import org.bigbluebutton.modules.present.events.NavigationEvent;
			import org.bigbluebutton.common.Images;
			import flexlib.mdi.events.MDIWindowEvent;		
			import mx.core.Application;
			import mx.binding.utils.BindingUtils;

			import mx.collections.ArrayCollection;
			import mx.rpc.events.*;
            import mx.managers.PopUpManager;
            import mx.containers.TitleWindow;
            import flash.geom.Point;
             			
            public static const TITLE:String = "Presentation";
			private var images:Images = new Images();

			[Bindable] private var uploadIcon:Class = images.upload;
			[Bindable] private var forwardIcon:Class = images.forward;
			[Bindable] private var backwardIcon:Class = images.backward;
			[Bindable] private var thumbnailIcon:Class = images.thumbnails;
			[Bindable] private var magnifierIcon:Class = images.magnifier;
			[Bindable] public var fitToWidthIcon:Class = images.magnifier;
			[Bindable] public var fitToActualSizeIcon:Class = images.mag_reset;
						
			[Bindable] private var thumbY:Number;
			public var uploadWindow:FileUploadWindow = null;
			private var slideManager:SlideManager = new SlideManager();
			
			private var _xPosition:int = 220;
			private var _yPosition:int = 0;
			
			[Bindable] private var CONTROL_BAR_HEIGHT:int = 45;
			private static const TOP_WINDOW_BORDER:int = 20;
			
			// Init to the size of the window.
			private var currentSlideWidth:int = 450;
			private var currentSlideHeight:int = 450;
						
			private var mouseDown:Boolean = false;
			[Bindable] private var isPresenter:Boolean = false;
			[Bindable] private var presentationLoaded:Boolean = false;
			[Bindable] private var currentPresentation:String = ResourceUtil.getInstance().getString('bbb.presentation.title');
			
			// The following code block is to deal with a bug in FLexLib 
			// with MDI windows not responding well to being maximized
			private var savedWindowWidth:Number;
			private var savedWindowHeight:Number;
			private var savedX:Number;
			private var savedY:Number;
			private var isMaximized:Boolean = false;
			
			private var fitToWindowTimer:Timer;
			
			override public function maximize():void{
				if (!isMaximized){
					savedWindowHeight = this.height;
					savedWindowWidth = this.width;
					savedX = this.x;
					savedY = this.y;    
					dispatchEvent(new MDIWindowEvent(MDIWindowEvent.MAXIMIZE, this));            	
                	isMaximized = true;
                	windowControls.maximizeRestoreBtn.toolTip = ResourceUtil.getInstance().getString('bbb.presentation.maximizeRestoreBtn.toolTip2');
				} else{
					this.width = savedWindowWidth;
					this.height = savedWindowHeight;
					this.x = savedX;
					this.y = savedY;
					isMaximized = false;
					windowControls.maximizeRestoreBtn.toolTip = ResourceUtil.getInstance().getString('bbb.presentation.maximizeRestoreBtn.toolTip');
				}					
				/*
				 * Need to have a timer to trigger resizing slide to window after the 
				 * window has been maximized/restored. The MAXIMIZE/RESTORE events are
				 * generated when the button is clicked so the width and height are not
				 * the ones we want.
				 */
				fitToWindowTimer = new Timer(1000);
				fitToWindowTimer.addEventListener(TimerEvent.TIMER, maximizeTimerHandler);
				fitToWindowTimer.start();
			}
			
			private function maximizeTimerHandler(event:TimerEvent):void {
				// When the window is maximized, we want to resize the slide maintaining the aspect ratio.			
				fitSlideToWindowMaintainingAspectRatio();
				
				// Move window so that it won't overlap with Participant's window.
				this.x = 220;
				this.y = 0;
				fitToWindowTimer.stop();
			}
			
			private function onCreationComplete():void{
				positionThumbnails();
				thumbnailWindow.addEventListener(FisheyeThumbnail.SLIDE_HIGHLIGHTED, onSlideHighlighted);
				thumbY = this.height - 160;
				addEventListener(MDIWindowEvent.RESIZE_END, onResizeEndEvent);
			}
			
			private function onResizeEndEvent(event:MDIWindowEvent):void {
				if (event.window == this) {
					fitSlideToWindowMaintainingAspectRatio();
				}
			}
			
			private function fitSlideToWindowMaintainingAspectRatio():void {
				// Initialize to snap to take the width of the window as the size of the slide.
				var slideWidth:int = this.width;
				var slideHeight:int = this.height;
				
				// If the height is smaller than the width, we use the height as the base to determine
				// the size of the slide.
				if (this.height < this.width) {
					slideHeight = this.height - CONTROL_BAR_HEIGHT - TOP_WINDOW_BORDER;
					slideWidth = ((currentSlideWidth * slideHeight)/currentSlideHeight);
				} else {
					slideWidth = this.width;
					slideHeight = ((currentSlideHeight * slideWidth)/currentSlideWidth);
				}
				
				LogUtil.debug("before: fitSlideToWindowMaintainingAspectRatio " + this.width + "," + this.height);
				this.width = slideWidth;
				this.height = slideHeight + CONTROL_BAR_HEIGHT + TOP_WINDOW_BORDER;
				LogUtil.debug("after: fitSlideToWindowMaintainingAspectRatio " + "," + this.width + "," + this.height);
				
				sendWindowResizedEvent(slideWidth, slideHeight);
			}
			
			/*
			 * Notify the slide container telling it the available dimensions to display the slide.
			 */
			private function sendWindowResizedEvent(slideWidth:int, slideHeight:int):void {
				var dispatcher:Dispatcher = new Dispatcher();
				var dispEvent:WindowResizedEvent = new WindowResizedEvent(WindowResizedEvent.PRESENTATION_WINDOW_RESIZED_EVENT);
				dispEvent.width = slideWidth;
				dispEvent.height = slideHeight;
				dispatcher.dispatchEvent(dispEvent);
			}
			
			private function handleDisplaySlideEvent(event:DisplaySlideEvent):void {
				currentSlideWidth = event.slideWidth;
				currentSlideHeight = event.slideHeight;
				fitSlideToWindowMaintainingAspectRatio();			
			}
			
			private function onKeyUp(event:KeyboardEvent):void{
				switch (event.keyCode) {
				case Keyboard.LEFT:
				case Keyboard.UP:
				case Keyboard.PAGE_UP:				
					gotoPreviousSlide();		
				break;
				case Keyboard.DOWN:
				case Keyboard.RIGHT: 
				case Keyboard.SPACE:
				case Keyboard.PAGE_DOWN:
				case Keyboard.ENTER:
					gotoNextSlide();
				break; 
			}
			}
			
			private function positionThumbnails():void{
				this.thumbnailWindow.y = this.mainCanvas.height*0.9;
			}
			
			public function get xPosition():int {
				return _xPosition;
			}
			
			public function get yPosition():int {
				return _yPosition;
			}
			
			public function set xPosition(x:int):void {
				_xPosition = x;
			}
			
			public function set yPosition(y:int):void {
				_yPosition = y;
			}	
			
			[Bindable] private var _defaultWidth:int = 600;
			[Bindable] private var _defaultHeight:int = 450;
			
			public function get defaultWidth():int{
				return _defaultWidth;
			}
			
			public function get defaultHeight():int{
				return _defaultHeight;
			}
			
			public function set defaultHeight(height:int):void{
				this._defaultHeight = height;
			}
			
			public function set defaultWidth(width:int):void{
				this._defaultWidth = width;
			}
			
			public function resetWidthAndHeight():void{
				this.width = _defaultWidth;
				this.height = _defaultHeight;
			}
			
			private function hideThumbnails():void{
				if (!mouseDown && mouseY > this.height*0.90 && isPresenter){
					thumbnailWindow.y = this.height - 270;
					thumbnailWindow.visible = true;
				} else if (slideView.slides != null){
					slideNumLbl.text = "" + ResourceUtil.getInstance().getString('bbb.presentation.pages', [(slideView.selectedSlide + 1), slideView.slides.length]);
				}
			}
			
			private function onSlideHighlighted(e:Event):void{
				if (slideView.visible)
				slideNumLbl.text = "" + ResourceUtil.getInstance().getString('bbb.presentation.pages', [(thumbnailWindow.slideNumber + 1), slideView.slides.length]);
			}
			
			/**
			 * Hide the thumbnails in case the user moves the mouse outside this window
			 */
			private function hideThumbs():void{
				if (slideView.slides != null)
					slideNumLbl.text = "" + ResourceUtil.getInstance().getString('bbb.presentation.pages', [(slideView.selectedSlide + 1), slideView.slides.length]);
				
				thumbnailWindow.visible = false;
			}
			
			private function showThumbs():void {
				 if (isPresenter){
					//readjust the position of the thumbnails in case the window was resized
					//Note: this is necessary because the MDIWindow does not let you listen to the resize() event!
					thumbY = this.height - 150;
					thumbnailWindow.visible = true;
				}				
			}
			
			private function onSliderZoom():void {
				dispatchResizeEvent(zoomSlider.value);
			}
			
			private function dispatchResizeEvent(newSize:int):void {
				var presentEvent:PresenterCommands = new PresenterCommands(PresenterCommands.RESIZE);
				presentEvent.newSizeInPercent = newSize;
				dispatchEvent(presentEvent);
			}
			
			private function onResetZoom():void {
				zoomSlider.value = 100;
				dispatchResizeEvent(zoomSlider.value);
			}
			
			private function handleSlideResizedEvent(e:SlideResizedEvent):void{
				zoomSlider.value = e.percent;
			}
			
			private function onCanvasClick():void{
				thumbnailWindow.visible = false;
			}
						
			private function becomePresenter(e:MadePresenterEvent):void{
				uploadPres.visible = true;
				this.isPresenter = true;
			
				if (presentationLoaded) {
	            	slideNumLbl.text = ResourceUtil.getInstance().getString('bbb.presentation.pages', [(slideView.selectedSlide + 1), slideView.slides.length]);	
					backButton.visible = true;
					forwardButton.visible = true;	
					zoomSlider.visible = true;
					btnResetZoom.visible = true;
					thumbnailWindow.fisheye.selectedIndex = 0;
					thumbnailWindow.setDataProvider(slideView.slides);
				}
				
				presenterNameLabel.text = "";
				presenterNameLabel.visible = false;
				thumbnailWindow.visible = true;
				thumbnailWindow.setFisheyeVisibility(true);
			}
			
			private function becomeViewer(e:MadePresenterEvent):void{
				this.isPresenter = false;
				uploadPres.visible = false;
				if (presentationLoaded) {
           			slideNumLbl.text = ResourceUtil.getInstance().getString('bbb.presentation.pages', [(slideView.selectedSlide + 1), slideView.slides.length]);	
					backButton.visible = false;
					forwardButton.visible = false;	
					zoomSlider.visible = false;	
					btnResetZoom.visible = false;
				}
				dispatchEvent(new UploadEvent(UploadEvent.CLOSE_UPLOAD_WINDOW));
				
				presenterNameLabel.visible = true;
				if (e.presenterName != null) presenterNameLabel.text = ResourceUtil.getInstance().getString('bbb.presentation.presenting', [e.presenterName]);
				thumbnailWindow.visible = false;
				thumbnailWindow.setFisheyeVisibility(false);
			}
			
			private function handlePresentationLoadedEvent(e:PresentationEvent):void
			{	
				if (e.presentationName == currentPresentation) return;
				currentPresentation = e.presentationName;
				
				presentationLoaded = true;
				slideView.slides = e.slides.slides;         	
	            slideNumLbl.text = (slideView.selectedSlide + 1) + " of " + slideView.slides.length;		
				slideView.visible = true;		
	
				if (slideManager != null) slideManager.clear();	
					
				if (isPresenter) {
					backButton.visible = true;
					forwardButton.visible = true;
					zoomSlider.visible = true;
					btnResetZoom.visible = true;
					var shareEvent:PresenterCommands = new PresenterCommands(PresenterCommands.SHARE_PRESENTATION_COMMAND);
					shareEvent.presentationName = e.presentationName;
					shareEvent.share = true;
					dispatchEvent(shareEvent);
					
					thumbnailWindow.fisheye.selectedIndex = 0;
					thumbnailWindow.setDataProvider(slideView.slides);
	
				} else {
					dispatchEvent(new SlideEvent(SlideEvent.LOAD_CURRENT_SLIDE));
				}
			}
			
			private function gotoPage(e:NavigationEvent):void{
				slideView.selectedSlide = e.pageNumber;
				slideNumLbl.text = ResourceUtil.getInstance().getString('bbb.presentation.pages', [(e.pageNumber + 1), slideView.slides.length]);
				
				if (e.pageNumber <= slideView.slides.length-1 && e.pageNumber >= 0)
					slideManager.load(slideView.slides.getItemAt(e.pageNumber) as Slide); 
				
				if (e.pageNumber == 0) {
					backButton.enabled = false;
				} else {
					backButton.enabled = true;
				}
				
				if (e.pageNumber < slideView.slides.length - 1) {
					forwardButton.enabled = true;
				} else {
					forwardButton.enabled = false;
				}
				
//				zoomSlider.value = 100;
//				dispatchEvent(new ZoomEvent(ZoomEvent.RESTORE));
//				dispatchEvent(new MoveEvent(MoveEvent.MOVE));
			}
			
			private function clearPresentation(e:UploadEvent):void{
				slideView.visible = false;		
				slideView.selectedSlide = 0;
				slideNumLbl.text = "";
				presenterNameLabel.text = "";
				presenterNameLabel.visible = false;
			}
			
			private function enableTextSelection():void{
				slideView.turnOnTextSelection();
			}

			private function gotoPreviousSlide():void {
				if ((slideView.selectedSlide - 1) >= 0)
					dispatchEvent(new PresenterCommands(PresenterCommands.GOTO_SLIDE, slideView.selectedSlide - 1));
			}
			
			private function gotoNextSlide():void {
				if ((slideView.selectedSlide + 1) < slideView.slides.length)
					dispatchEvent(new PresenterCommands(PresenterCommands.GOTO_SLIDE, slideView.selectedSlide + 1));
			}
		]]>
	</mx:Script>
	 
	<mx:Fade id="thumbFadeIn" alphaFrom="1" alphaTo="0" duration="100" />
	<mx:Fade id="thumbFadeOut" alphaFrom="0" alphaTo="1" duration="100" />
	
	<mx:Canvas id="mainCanvas" width="100%" height="100%" resize="positionThumbnails()" mouseDown="mouseDown = true"
		mouseUp="mouseDown = false" verticalScrollPolicy="off" horizontalScrollPolicy="off" click="onCanvasClick()">
		<thumb:SlideView id="slideView" width="100%" height="100%" visible="false"/>
		<thumb:FisheyeThumbnail y="{thumbY}" id="thumbnailWindow" width="100%" height="100%" visible="false" backgroundAlpha="1" 
			verticalScrollPolicy="off" horizontalScrollPolicy="off" verticalCenter="top"/>
	</mx:Canvas>
			    
    <mx:ApplicationControlBar id="presCtrlBar" width="100%" height="{CONTROL_BAR_HEIGHT}">
    	  <mx:Button id="uploadPres" icon="{uploadIcon}" visible="false" width="20" height="20"
    	   		toolTip="{ResourceUtil.getInstance().getString('bbb.presentation.uploadPresBtn')}" click="dispatchEvent(new UploadEvent(UploadEvent.OPEN_UPLOAD_WINDOW))"/>    
    	  <mx:Label id="presenterNameLabel" visible="false" text=""/>
    	  <mx:Spacer width="10%" id="spacer1"/>
    	  <mx:Button id="backButton" icon="{backwardIcon}" visible="false" width="30" height="20"
    	   		toolTip="Previous slide." click="gotoPreviousSlide()"/>
    	  <mx:Label id="slideNumLbl" text=""/>    
    	  <mx:Button id="forwardButton" icon="{forwardIcon}" visible="false" width="30" height="20"
    	   		toolTip="Next slide" click="gotoNextSlide()"/>    				
    	<mx:Spacer width="10%" id="spacer2"/>
    	<mx:HSlider id="zoomSlider" visible="false"
    		minimum="100" maximum="400" value="100" dataTipPlacement="top" labels="['100%','400%']"
    		allowTrackClick="true" liveDragging="true" change="onSliderZoom()" width="100" />
    	<mx:Spacer width="100%" id="spacer3"/>
    	<mx:Button id="btnResetZoom" icon="{magnifierIcon}" visible="false" width="20" height="20" 
    		toolTip="Reset Zoom" click="onResetZoom()"/>
    	<!--<mx:Button id="btnSelection" click="enableTextSelection()" />-->
    </mx:ApplicationControlBar>
</pres:MDIWindow>