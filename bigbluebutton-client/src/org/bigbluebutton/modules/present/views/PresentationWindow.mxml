<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton - http://www.bigbluebutton.org
  
  Copyright (c) 2008-2009 by respective authors (see below). All rights reserved.
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 3 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, If not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
--> 


<pres:MDIWindow xmlns:mx="http://www.adobe.com/2006/mxml"  
	xmlns:thumb="org.bigbluebutton.modules.present.views.*"
	xmlns:pres="flexlib.mdi.containers.*"
	paddingBottom="0" paddingTop="0" 
	paddingLeft="0" paddingRight="0" 
	showCloseButton="false"
    verticalScrollPolicy="off" horizontalScrollPolicy="off"
	implements="org.bigbluebutton.common.IBbbModuleWindow"
	mouseMove="thumbnailDisplay()"
	creationComplete="init()"
	rollOut="hideThumbs()" xmlns:mate="http://mate.asfusion.com/"
	width="450" height="450" x="220" y="0"
	title="Presentation" keyUp="onKeyUp(event)">
	
	<mate:Dispatcher id="globalDispatcher" />
	<mate:Listener type="{MadePresenterEvent.SWITCH_TO_PRESENTER_MODE}" method="becomePresenter" />
	<mate:Listener type="{MadePresenterEvent.SWITCH_TO_VIEWER_MODE}" method="becomeViewer" />
	<mate:Listener type="{PresentationEvent.PRESENTATION_LOADED}" method="handlePresentationLoadedEvent" />
	<mate:Listener type="{NavigationEvent.GOTO_PAGE}" method="gotoPage" />
	<mate:Listener type="{UploadEvent.CLEAR_PRESENTATION}" method="clearPresentation" />
	<mate:Listener type="{ZoomEvent.ZOOM}" method="zoomCallback" />
	
	<mx:Script>
		<![CDATA[
			import org.bigbluebutton.main.events.MadePresenterEvent;
			import org.bigbluebutton.modules.present.events.SlideEvent;
			import mx.controls.Alert;
			import org.bigbluebutton.modules.present.managers.Slide;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			import mx.effects.Move;
			import org.bigbluebutton.modules.present.events.MoveEvent;
			import org.bigbluebutton.modules.present.events.ZoomEvent;
			import org.bigbluebutton.modules.present.managers.SlideManager;
			import org.bigbluebutton.modules.present.events.PresentationEvent;
			import org.bigbluebutton.modules.present.events.PresenterCommands;
			import org.bigbluebutton.modules.present.events.UploadEvent;
			import org.bigbluebutton.modules.present.events.NavigationEvent;
			import org.bigbluebutton.common.Images;
			import flexlib.mdi.events.MDIWindowEvent;
			import org.bigbluebutton.main.events.AddHighligtherCanvasEvent;
		
			import mx.core.Application;
			import mx.binding.utils.BindingUtils;

			import mx.collections.ArrayCollection;
			import mx.rpc.events.*;
            import mx.managers.PopUpManager;
            import mx.containers.TitleWindow;
            import flash.geom.Point;
             			
            public static const TITLE:String = "Presentation";
			private var images:Images = new Images();

			[Bindable] private var uploadIcon : Class = images.upload;
			[Bindable] private var forwardIcon : Class = images.forward;
			[Bindable] private var backwardIcon : Class = images.backward;
			[Bindable] private var thumbnailIcon : Class = images.thumbnails;
			[Bindable] private var magnifierIcon : Class = images.magnifier;
						
			[Bindable] private var thumbY:Number;
			public var uploadWindow:FileUploadWindow = null;
			private var slideManager:SlideManager = new SlideManager();
			
			private var _xPosition:int = 220;
			private var _yPosition:int = 0;
			
			private var mouseDown:Boolean = false;
			[Bindable] private var isPresenter:Boolean = false;
			[Bindable] private var presentationLoaded:Boolean = false;
			private var currentPresentation:String = "";
			
			//The following code block is to deal with a bug in FLexLib with MDI windows not responding well to being maximized
			private var savedWindowWidth:Number;
			private var savedWindowHeight:Number;
			private var savedX:Number;
			private var savedY:Number;
			private var isMaximized:Boolean = false;
			override public function maximize():void{
				if (!isMaximized){
					savedWindowHeight = this.height;
					savedWindowWidth = this.width;
					savedX = this.x;
					savedY = this.y;
                	dispatchEvent(new MDIWindowEvent(MDIWindowEvent.MAXIMIZE, this));
                	isMaximized = true;
				} else{
					this.width = savedWindowWidth;
					this.height = savedWindowHeight;
					this.x = savedX;
					this.y = savedY;
					isMaximized = false;
					//dispatchEvent(new MDIWindowEvent(MDIWindowEvent.RESTORE, this));
				}	
			}
			
			private function init():void{
				positionThumbnails();
				thumbnailWindow.addEventListener(FisheyeThumbnail.SLIDE_HIGHLIGHTED, onSlideHighlighted);
				thumbY = this.height - 160;
				//slideView.addEventListener(SlideViewMediator.ZOOM, scrollZoom);
			}
			
			private function onKeyUp(e:KeyboardEvent):void{
				if (e.keyCode == Keyboard.LEFT){
					
				} else if (e.keyCode == Keyboard.RIGHT){
					dispatchEvent(new PresenterCommands(PresenterCommands.GOTO_SLIDE, slideView.selectedSlide + 1));
				}
			}
			
			private function positionThumbnails():void{
				this.thumbnailWindow.y = this.mainCanvas.height*0.9;
			}
			
			public function get xPosition():int {
				return _xPosition;
			}
			
			public function get yPosition():int {
				return _yPosition;
			}
			
			public function set xPosition(x:int):void {
				_xPosition = x;
			}
			
			public function set yPosition(y:int):void {
				_yPosition = y;
			}	
			
			[Bindable] private var _defaultWidth:int = 450;
			[Bindable] private var _defaultHeight:int = 450;
			
			public function get defaultWidth():int{
				return _defaultWidth;
			}
			
			public function get defaultHeight():int{
				return _defaultHeight;
			}
			
			public function set defaultHeight(height:int):void{
				this._defaultHeight = height;
			}
			
			public function set defaultWidth(width:int):void{
				this._defaultWidth = width;
			}
			
			public function resetWidthAndHeight():void{
				this.width = _defaultWidth;
				this.height = _defaultHeight;
			}
			
			private function thumbnailDisplay():void{
				if (!mouseDown && mouseY > this.height*0.8 && isPresenter){
					thumbnailWindow.y = this.height - 270;
					thumbnailWindow.visible = true;
				} else if (slideView.slides != null){
					slideNumLbl.text = "" + (slideView.selectedSlide + 1) + " of " + slideView.slides.length;
				}
			}
			
			private function onSlideHighlighted(e:Event):void{
				if (slideView.visible)
				slideNumLbl.text = "" + (thumbnailWindow.slideNumber+1) + " of " + slideView.slides.length;
			}
			
			/**
			 * Hide the thumbnails in case the user moves the mouse outside this window
			 */
			private function hideThumbs():void{
				if (slideView.slides != null)
					slideNumLbl.text = "" + (slideView.selectedSlide + 1) + " of " + slideView.slides.length;
				
				thumbnailWindow.visible = false;
			}
			
			private function showThumbs():void {
				 if (isPresenter){
					//readjust the position of the thumbnails in case the window was resized
					//Note: this is necessary because the MDIWindow does not let you listen to the resize() event!
					thumbY = this.height - 150;
					thumbnailWindow.visible = true;
				}				
			}
			
			private function sliderZoom():void{
				var newWidth:Number = slideView.imageCanvas.width * (zoomSlider.value/100);
				var newHeight:Number = slideView.imageCanvas.height * (zoomSlider.value/100);
				
				var xPercent:Number = newWidth / slideView.imageCanvas.width;
				var yPercent:Number = newHeight / slideView.imageCanvas.height;
				
				var presentEvent:PresenterCommands = new PresenterCommands(PresenterCommands.ZOOM);
				presentEvent.xPercent = xPercent;
				presentEvent.yPercent = yPercent;
				dispatchEvent(presentEvent);
			}
			
			private function zoomCallback(e:Event):void{
				//zoomSlider.value = (slideView.myLoader.width / slideView.imageCanvas.width) * 100;
			}
			
			private function onCanvasClick():void{
				thumbnailWindow.visible = false;
			}
			
			private function addHighlighter(event:AddHighligtherCanvasEvent):void{
				/*var highlighter:Canvas = event.canvas;
				highlighter.x = mainCanvas.x;
				highlighter.y = mainCanvas.y;
				highlighter.width = mainCanvas.width;
				highlighter.height = mainCanvas.height;
				this.addChild(highlighter);
				this.addChildAt(highlighter, numChildren-1);*/
			}
			
			private function becomePresenter(e:MadePresenterEvent):void{
				uploadPres.visible = true;
				this.isPresenter = true;
			
				if (presentationLoaded) {
	            	slideNumLbl.text = (slideView.selectedSlide + 1) + " of " + slideView.slides.length;	
					backButton.visible = true;
					forwardButton.visible = true;	
					zoomSlider.visible = true;
					btnResetZoom.visible = true;
					thumbnailWindow.fisheye.selectedIndex = 0;
					thumbnailWindow.setDataProvider(slideView.slides);
				}
				
				presenterNameLabel.text = ResourceUtil.getInstance().getString('bbb.presentation.presenting', [e.presenterName]);
				presenterNameLabel.visible = true;
				thumbnailWindow.visible = true;
				thumbnailWindow.setFisheyeVisibility(true);
			}
			
			private function becomeViewer(e:MadePresenterEvent):void{
				this.isPresenter = false;
				uploadPres.visible = false;
				if (presentationLoaded) {
	            	slideNumLbl.text = (slideView.selectedSlide + 1) + " of " + slideView.slides.length;	
					backButton.visible = false;
					forwardButton.visible = false;	
					zoomSlider.visible = false;	
					btnResetZoom.visible = false;
				}
				dispatchEvent(new UploadEvent(UploadEvent.CLOSE_UPLOAD_WINDOW));
				
				presenterNameLabel.text = "";
				presenterNameLabel.visible = false;
				thumbnailWindow.visible = false;
				thumbnailWindow.setFisheyeVisibility(false);
			}
			
			private function handlePresentationLoadedEvent(e:PresentationEvent):void
			{	
				if (e.presentationName == currentPresentation) return;
				currentPresentation = e.presentationName;
				
				presentationLoaded = true;
				slideView.slides = e.slides.slides;         	
	            slideNumLbl.text = (slideView.selectedSlide + 1) + " of " + slideView.slides.length;		
				slideView.visible = true;		
	
				if (slideManager != null) slideManager.clear();	
					
				if (isPresenter) {
					//removeFileUploadPopup();
					backButton.visible = true;
					forwardButton.visible = true;
					zoomSlider.visible = true;
					
					var shareEvent:PresenterCommands = new PresenterCommands(PresenterCommands.SHARE_PRESENTATION_COMMAND);
					shareEvent.presentationName = e.presentationName;
					shareEvent.share = true;
					dispatchEvent(shareEvent);
					var gotoSlideEvent:PresenterCommands = new PresenterCommands(PresenterCommands.GOTO_SLIDE);
					gotoSlideEvent.slideNumber = 0;
					dispatchEvent(gotoSlideEvent);
					dispatchEvent(new PresenterCommands(PresenterCommands.NOTIFY_LOADED));
					
					thumbnailWindow.fisheye.selectedIndex = 0;
					thumbnailWindow.setDataProvider(slideView.slides);
	
				} else {
					dispatchEvent(new SlideEvent(SlideEvent.LOAD_CURRENT_SLIDE));
				}
			}
			
			private function gotoPage(e:NavigationEvent):void{
				slideView.selectedSlide = e.pageNumber;
				slideNumLbl.text = ResourceUtil.getInstance().getString('bbb.presentation.pages', [(e.pageNumber + 1), slideView.slides.length]);
				
				if (e.pageNumber <= slideView.slides.length-1 && e.pageNumber >= 0)
					slideManager.load(slideView.slides.getItemAt(e.pageNumber) as Slide); 
				
				if (e.pageNumber == 0) {
					backButton.enabled = false;
				} else {
					backButton.enabled = true;
				}
				
				if (e.pageNumber < slideView.slides.length - 1) {
					forwardButton.enabled = true;
				} else {
					forwardButton.enabled = false;
				}
				
				zoomSlider.value = 100;
				dispatchEvent(new ZoomEvent(ZoomEvent.RESTORE));
				dispatchEvent(new MoveEvent(MoveEvent.MOVE, 0, 0));
			}
			
			private function clearPresentation(e:UploadEvent):void{
				slideView.visible = false;		
				slideView.selectedSlide = 0;
				slideNumLbl.text = "";
				presenterNameLabel.text = "";
				presenterNameLabel.visible = false;
			}

		]]>
	</mx:Script>
	 
	<mx:Fade id="thumbFadeIn" alphaFrom="1" alphaTo="0" duration="100" />
	<mx:Fade id="thumbFadeOut" alphaFrom="0" alphaTo="1" duration="100" />
	
	<mx:Canvas id="mainCanvas" width="100%" height="100%" resize="positionThumbnails()" mouseDown="mouseDown = true" 
		mouseUp="mouseDown = false" verticalScrollPolicy="off" horizontalScrollPolicy="off" click="onCanvasClick()">
		<thumb:SlideView id="slideView" width="100%" height="100%" visible="false"/>
		<thumb:FisheyeThumbnail y="{thumbY}" id="thumbnailWindow" width="100%" height="100%" visible="false" backgroundAlpha="1" 
			verticalScrollPolicy="off" horizontalScrollPolicy="off" verticalCenter="top"/>
	</mx:Canvas>
			
    
    <mx:ApplicationControlBar id="presCtrlBar" width="100%" >
    	  <mx:Button id="uploadPres" icon="{uploadIcon}" visible="false" width="20" height="20"
    	   		toolTip="Upload a document for presentation." click="dispatchEvent(new UploadEvent(UploadEvent.OPEN_UPLOAD_WINDOW))"/>    
    	  <mx:Label id="presenterNameLabel" visible="false" text=""/>
    	  <mx:Spacer width="10%" id="spacer1"/>
    	  <mx:Button id="backButton" icon="{backwardIcon}" visible="false" width="40" height="20"
    	   		toolTip="Previous slide." click="dispatchEvent(new PresenterCommands(PresenterCommands.GOTO_SLIDE, slideView.selectedSlide - 1))"/>
    	  <mx:Label id="slideNumLbl" text=""/>    
    	  <mx:Button id="forwardButton" icon="{forwardIcon}" visible="false" width="40" height="20"
    	   		toolTip="Next slide" click="dispatchEvent(new PresenterCommands(PresenterCommands.GOTO_SLIDE, slideView.selectedSlide + 1))"/>    				
    	<mx:Spacer width="10%" id="spacer2"/>
    	<mx:HSlider id="zoomSlider" visible="false"
    		minimum="100" maximum="300" value="100" 
    		dataTipPlacement="right" labels="['100%','300%']"
    		allowTrackClick="true" liveDragging="true" change="sliderZoom()" width="100" />
    	<mx:Button id="btnResetZoom" icon="{magnifierIcon}" visible="false" width="20" height="20" 
    		toolTip="Reset Zoom" click="dispatchEvent(new PresenterCommands(PresenterCommands.RESET_ZOOM))"/>
    </mx:ApplicationControlBar>
    
    <mate:Debugger level="{Debugger.ALL}" />
    <mate:Listener type="{AddHighligtherCanvasEvent.ADD_HIGHLIGHTER_CANVAS}" method="addHighlighter" />
</pres:MDIWindow>