<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton - http://www.bigbluebutton.org
  
  Copyright (c) 2008-2009 by respective authors (see below). All rights reserved.
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 3 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, If not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
--> 

<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" 
    width="100%" height="100%" 
    horizontalAlign="center" paddingBottom="5" 
    creationComplete="init()" verticalScrollPolicy="off" horizontalScrollPolicy="off" 
    xmlns:panzoom="com.adobe.wheelerstreet.fig.panzoom.*" xmlns:local="*"
    resize="onResize()" xmlns:mate="http://mate.asfusion.com/" rollOut="hideCursor()">
    
    <mate:Listener type="{ZoomEvent.ZOOM}" method="handleZoomEvent" />
    <mate:Listener type="{ZoomEvent.RESTORE}" method="handleRestoreSlideEvent" />
    <mate:Listener type="{MoveEvent.MOVE}" method="handleMoveEvent" />
    <mate:Listener type="{SlideEvent.SLIDE_LOADED}" method="handleSlideLoadedEvent" />
    <mate:Listener type="{MadePresenterEvent.SWITCH_TO_PRESENTER_MODE}" method="handleSwitchToPresenterEvent" />
    <mate:Listener type="{MadePresenterEvent.SWITCH_TO_VIEWER_MODE}" method="handleSwitchToViewerEvent" />
    <mate:Listener type="{CursorEvent.UPDATE_CURSOR}" method="handleUpdateCursorEvent" />
    <mate:Listener type="{ZoomEvent.RESIZE}" method="handleResizeSlideEvent" />
        
	<mx:Script>
		<![CDATA[
			import org.bigbluebutton.modules.present.events.CursorEvent;
			import org.bigbluebutton.main.events.MadePresenterEvent;
			import org.bigbluebutton.modules.present.events.SlideEvent;
			import org.bigbluebutton.modules.present.events.MoveEvent;
			import org.bigbluebutton.modules.present.events.ZoomEvent;
			import org.bigbluebutton.modules.present.events.PresenterCommands;
			import mx.controls.Button;
			import mx.collections.ArrayCollection;
			import mx.binding.utils.BindingUtils;
			import flash.events.Event;
			import mx.events.ListEvent;
			import mx.events.FlexEvent;

			private var startDragMouseX:Number;
			private var startDragMouseY:Number;
			private var startDragX:Number;
			private var startDragY:Number;
			
			private var xPercent:Number;
			private var yPercent:Number;
			
			private var cursor:Shape;
				
        	[Bindable] public var slides:ArrayCollection;
			[Bindable] public var selectedSlide:int=0;
			[Bindable] private var isPresenter:Boolean = false;
				
			private function init():void {
				myLoader.width = imageCanvas.width;
				myLoader.height = imageCanvas.height;
				
				cursor = new Shape();
				cursor.graphics.lineStyle(6, 0xFF0000, 0.6);
				cursor.graphics.drawCircle(0,0,3);
				imageCanvas.rawChildren.addChild(cursor);
				cursor.visible = false;
			}
			
			private function onZoom(e:MouseEvent):void {
				var presentEvent:PresenterCommands = new PresenterCommands(PresenterCommands.ZOOM);
				presentEvent.zoomPercentage = e.delta/100;
				dispatchEvent(presentEvent);
			}
		
			private function onMouseDown(e:MouseEvent):void{		
				startDragX = myLoader.content.x;
				startDragY = myLoader.content.y;
				startDragMouseX = imageCanvas.mouseX;
				startDragMouseY = imageCanvas.mouseY;
				addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			}
			
			private function onMouseMove(e:MouseEvent):void{	
				var newX:Number = startDragX + (imageCanvas.mouseX - startDragMouseX);
				var newY:Number = startDragY + (imageCanvas.mouseY - startDragMouseY);
				
				var xOffset:Number = newX / imageCanvas.width;
				var yOffset:Number = newY / imageCanvas.height;
			
				var presentEvent:PresenterCommands = new PresenterCommands(PresenterCommands.MOVE);
				presentEvent.xOffset = xOffset;
				presentEvent.yOffset = yOffset;
				dispatchEvent(presentEvent);
			}
		
			private function onMouseUp(e:MouseEvent):void {		
				removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			}
			
			private function onMouseOut(e:MouseEvent):void {
				removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			}
			
			public function onResize():void {
				if (myLoader.content == null) return;
				myLoader.width = myLoader.content.width;
				myLoader.height = myLoader.content.height;
			}
			
			private function handleZoomEvent(e:ZoomEvent):void {
				myLoader.content.scaleX += e.zoomPercentage;
				myLoader.content.scaleY += e.zoomPercentage;
				myLoader.width = myLoader.content.width;
				myLoader.height = myLoader.content.height;
			}
			
			private function handleResizeSlideEvent(e:ZoomEvent):void {
				myLoader.content.scaleX = e.zoomPercentage;
				myLoader.content.scaleY = e.zoomPercentage;
				myLoader.width = myLoader.content.width;
				myLoader.height = myLoader.content.height;
			}
			
			private function handleMoveEvent(e:MoveEvent):void {
				LogUtil.debug("Handling moveEvent");
				if (myLoader.content == null) return;
				var newX:Number = e.slideXPosition * imageCanvas.width;
				var newY:Number = e.slideYPosition * imageCanvas.height;
				
				if (newX <= 0) myLoader.content.x = newX;
				if (newY <= 0) myLoader.content.y = newY;
			}
			
			private function handleSlideLoadedEvent(e:SlideEvent):void {
				myLoader.source = e.slide;
			}
			
			private function handleRestoreSlideEvent(e:ZoomEvent):void {
				if (myLoader.content == null) return;
				myLoader.content.scaleX = 1;
				myLoader.content.scaleY = 1;
				myLoader.content.x = 0;
				myLoader.content.y = 0;
			}
			
			private function handleSwitchToViewerEvent(e:MadePresenterEvent):void {
				this.isPresenter = false;
				removeEventListener(MouseEvent.MOUSE_WHEEL, onZoom);
				myLoader.removeEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
				myLoader.removeEventListener(MouseEvent.MOUSE_UP, onMouseUp);
				myLoader.removeEventListener(MouseEvent.MOUSE_OUT, onMouseOut);
				imageCanvas.removeEventListener(MouseEvent.MOUSE_MOVE, sendCursorUpdate);
			}
			
			private function handleSwitchToPresenterEvent(e:MadePresenterEvent):void {
				this.isPresenter = true;
				addEventListener(MouseEvent.MOUSE_WHEEL, onZoom);
				myLoader.addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
				myLoader.addEventListener(MouseEvent.MOUSE_UP, onMouseUp);
				myLoader.addEventListener(MouseEvent.MOUSE_OUT, onMouseOut);
				imageCanvas.addEventListener(MouseEvent.MOUSE_MOVE, sendCursorUpdate);
			}
			
			public function sendCursorUpdate(e:MouseEvent):void {
				if (myLoader.content == null) return;
				var command:PresenterCommands = new PresenterCommands(PresenterCommands.SEND_CURSOR_UPDATE);
				command.xPercent = myLoader.mouseX / myLoader.content.width;
				command.yPercent = myLoader.mouseY / myLoader.content.height;
				dispatchEvent(command);
			}
		   	
			private function handleUpdateCursorEvent(e:CursorEvent):void {
				if (myLoader.content == null) return;
				
				cursor.x = myLoader.x + (e.xPercent * myLoader.content.width);
				cursor.y = myLoader.y + (e.yPercent * myLoader.content.height);
				
				//If cursor is off window, don't show it
				if (isCursorOffWindow(e)) cursor.visible = false;
				else cursor.visible = true;	
			}
			
			private function isCursorOffWindow(e:CursorEvent):Boolean {
				return (e.xPercent > 1 && e.yPercent > 1) || (cursor.x > this.width || cursor.y > this.height);
			}
			
			private function hideCursor():void{
				cursor.visible = false;
			}
			
		]]>
	</mx:Script>
	
	<mx:Canvas id="imageCanvas" width="100%" height="100%" verticalScrollPolicy="off" horizontalScrollPolicy="off">  
		<mx:SWFLoader id="myLoader" width="100%" height="100%" scaleContent="false" 
			maintainAspectRatio="true" showBusyCursor="true" completeEffect="Fade"/>    	
	</mx:Canvas>  
		      		 
</mx:VBox>
