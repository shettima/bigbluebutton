<?xml version="1.0" encoding="utf-8"?>
<MDIWindow xmlns="flexlib.mdi.containers.*" 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	implements="org.bigbluebutton.common.IBbbModuleWindow"
	creationComplete="init()"
	width="{camWidth + 6}" height="{camHeight + 73}" 
	title="Stream webcam" 
	backgroundImage="{bbbLogo}" 
	resizable="false">
	
	<mx:Script>
		<![CDATA[
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.modules.videoconf.events.StopBroadcastEvent;
			import org.bigbluebutton.modules.videoconf.events.StartBroadcastEvent;
			import mx.core.UIComponent;
			import mx.controls.Alert;
		
			private var _xPosition:int = 500;
			private var _yPosition:int = 500;
			
			private var images:Images = new Images();
			[Bindable] public var camIcon:Class = images.webcam;
			[Bindable] public var bbbLogo:Class = images.bbb_logo;
			[Bindable] private var resolutions:Array = new Array("320x240", "640x480");
			
			private var video:Video;
			public var streamName:String;
			[Bindable] private var camWidth:Number = 320;
			[Bindable] private var camHeight:Number = 240;
			private var _userrole:String;
			
			private function init():void{
				if (isPresenter()) showResControls(true);
				if (Camera.names.length > 1) showVideoControls(true);
				if (!isPresenter() && Camera.names.length == 1) startPublishing();
			}
			
			public function get xPosition():int {
				return _xPosition;
			}
			
			public function get yPosition():int {
				return _yPosition;
			}
			
			public function set xPosition(x:int):void {
				_xPosition = x;
			}
			
			public function set yPosition(y:int):void {
				_yPosition = y;
			}		
			
			private function startPublishing():void{
				var camera:Camera = Camera.getCamera(cmbCameraSelector.selectedIndex.toString());
				if (camera == null) return;
				
				setResolution();
				
				camera.setKeyFrameInterval(5);
				camera.setMode(camWidth,camHeight,15);
				camera.setQuality(0,0);
				
				video = new Video(camWidth, camHeight);
				video.attachCamera(camera);
				videoHolder.addChild(video);
				addChild(videoHolder);
				
				var e:StartBroadcastEvent = new StartBroadcastEvent();
				e.stream = this.streamName;
				e.camera = camera;
				dispatchEvent(e);
				
				showVideoControls(false);
				showResControls(false);
			}
			
			override public function close(event:MouseEvent=null):void{
				stopPublishing();
				super.close(event);
			}
			
			private function stopPublishing():void{
				var e:StopBroadcastEvent = new StopBroadcastEvent()
				e.stream = this.streamName;
				dispatchEvent(e);
			}
			
			private function setResolution():void{
				if (cmbResolution.selectedLabel == "320x240"){
					camHeight = 240;
					camWidth = 320;
				} else if (cmbResolution.selectedLabel == "640x480"){
					camHeight = 480;
					camWidth = 640;
				}
				this.streamName = cmbResolution.selectedLabel.concat(this.streamName);
			}
			
			public function set userrole(role:String):void{
				this._userrole = role;
			}
			
			private function isPresenter():Boolean{
				if (this._userrole == "MODERATOR" || this._userrole == "PRESENTER") return true;
				else return false;
			}
			
			private function showVideoControls(show:Boolean):void{
				if (show){
					bottomBar.visible = true;
					btnStartPublish.visible = true;
					cmbCameraSelector.visible = true;
				} else{
					bottomBar.visible = false;
					btnStartPublish.visible = false;
					cmbCameraSelector.visible = false;
				}
			}
			
			private function showResControls(show:Boolean):void{
				if (show) cmbResolution.visible = true;
				else cmbResolution.visible = false;
			}
			
		]]>
	</mx:Script>
	
	<mx:UIComponent id="videoHolder" width="100%" height="100%" />
	<mx:ControlBar id="bottomBar">
		<mx:Button id="btnStartPublish" toolTip="start streaming" icon="{camIcon}" click="startPublishing()" />
		<mx:ComboBox id="cmbCameraSelector" dataProvider="{Camera.names}" width="150" visible="false" />
		<mx:ComboBox id="cmbResolution" dataProvider="{resolutions}" width="20%" visible="false" />
	</mx:ControlBar>
</MDIWindow>
