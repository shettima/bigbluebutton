<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton - http://www.bigbluebutton.org
  
  Copyright (c) 2008-2009 by respective authors (see below). All rights reserved.
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 3 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, If not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
--> 

<EventMap xmlns:mx="http://www.adobe.com/2006/mxml" xmlns="http://mate.asfusion.com/">
	<mx:Script>
		<![CDATA[
			import org.bigbluebutton.main.events.ToolbarButtonEvent;
			import org.bigbluebutton.modules.viewers.view.events.StreamStartedEvent;
			import org.bigbluebutton.modules.videoconf.views.TiledVideoWindow;
			import org.bigbluebutton.modules.videoconf.events.BroadcastStoppedEvent;
			import org.bigbluebutton.modules.videoconf.events.StopBroadcastEvent;
			import org.bigbluebutton.modules.videoconf.events.BroadcastStartedEvent;
			import org.bigbluebutton.modules.videoconf.business.VideoProxy;
			import org.bigbluebutton.modules.videoconf.events.StartBroadcastEvent;
			import org.bigbluebutton.modules.videoconf.views.PublishWindow;
			import org.bigbluebutton.modules.videoconf.events.OpenPublishWindowEvent;
			import org.bigbluebutton.modules.videoconf.views.ToolbarButton;
			import mx.controls.Alert;
			import org.bigbluebutton.modules.viewers.view.events.ViewCameraEvent;
			import org.bigbluebutton.main.events.OpenWindowEvent;
			import org.bigbluebutton.modules.videoconf.views.VideoWindow;
			
			public var module:VideoconfModule;
			private var button:ToolbarButton;
			private var publishWindow:PublishWindow;
			public var proxy:VideoProxy;
			private var tiledWindow:TiledVideoWindow;
			
			private function viewCamera(e:ViewCameraEvent):void{
				var window:VideoWindow = new VideoWindow();
				window.startVideo(module.connection, e.stream);
				window.title = e.viewedName;
				
				var windowEvent:OpenWindowEvent = new OpenWindowEvent(OpenWindowEvent.OPEN_WINDOW_EVENT);
				windowEvent.window = window;
				globalDispatcher.dispatchEvent(windowEvent);
			}
			
			public function addToolbarButton():void{
				button = new ToolbarButton();	   	
				var event:ToolbarButtonEvent = new ToolbarButtonEvent(ToolbarButtonEvent.ADD);
				event.button = button;
				globalDispatcher.dispatchEvent(event);
			}
			
			private function openPublishWindow():void{
				publishWindow = new PublishWindow();
				publishWindow.streamName = module.userid.toString();
				publishWindow.userrole = module.role;
				
				var windowEvent:OpenWindowEvent = new OpenWindowEvent(OpenWindowEvent.OPEN_WINDOW_EVENT);
				windowEvent.window = publishWindow;
				globalDispatcher.dispatchEvent(windowEvent);
			}
			
			private function startPublishing(e:StartBroadcastEvent):void{
			    LogUtil.debug("Publishing stream to: " + proxy.connection.uri + "/" + e.stream);
				proxy.startPublishing(e);
				var broadcastEvent:BroadcastStartedEvent = new BroadcastStartedEvent();
				broadcastEvent.stream = e.stream;
				broadcastEvent.userid = module.userid;
				globalDispatcher.dispatchEvent(broadcastEvent);
			}
			
			private function stopPublishing(e:StopBroadcastEvent):void{
				proxy.stopBroadcasting(e);
				proxy = null;
				proxy = new VideoProxy(module.uri);
				var broadcastEvent:BroadcastStoppedEvent = new BroadcastStoppedEvent();
				broadcastEvent.stream = e.stream;
				broadcastEvent.userid = module.userid;
				globalDispatcher.dispatchEvent(broadcastEvent);
				
				//Make toolbar button visible again
				button.show();
			}
			
			public function addTiledWindow():void{
				tiledWindow = new TiledVideoWindow();
				tiledWindow.connection = module.connection;
				
				var windowEvent:OpenWindowEvent = new OpenWindowEvent(OpenWindowEvent.OPEN_WINDOW_EVENT);
				windowEvent.window = tiledWindow;
				globalDispatcher.dispatchEvent(windowEvent);
				
			}
			
			private function streamStarted(e:StreamStartedEvent):void{
				if (tiledWindow == null) return;
				tiledWindow.addStream(e.user, e.stream);
			}
		]]>
	</mx:Script>
	
	<EventHandlers type="{OpenPublishWindowEvent.OPEN_PUBLISH_WINDOW}">
		<InlineInvoker method="openPublishWindow" />
	</EventHandlers>
	
	<EventHandlers type="{StartBroadcastEvent.START_BROADCAST}" >
		<InlineInvoker method="startPublishing" arguments="{event}" />
	</EventHandlers>
	
	<EventHandlers type="{StopBroadcastEvent.STOP_BROADCASTING}" >
		<InlineInvoker method="stopPublishing" arguments="{event}" />
	</EventHandlers>
	
	<Listener type="{ViewCameraEvent.VIEW_CAMERA_EVENT}" method="viewCamera" />
	<Listener type="{StreamStartedEvent.STREAM_STARTED}" method="streamStarted" />
</EventMap>
