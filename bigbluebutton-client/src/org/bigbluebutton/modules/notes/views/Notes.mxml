<?xml version="1.0" encoding="utf-8"?>
<!--
Notes.mxml is the main view of the SharedNotes application
-->
<MDIWindow xmlns="flexlib.mdi.containers.*" xmlns:mx="http://www.adobe.com/2006/mxml" 
	width="400" height="300" xmlns:mate="http://mate.asfusion.com/" 
	implements="org.bigbluebutton.common.IBbbModuleWindow" creationComplete="startTimer()">
	
	<mx:Script>
		<![CDATA[
			import flexlib.mdi.events.MDIWindowEvent;
			import org.bigbluebutton.modules.notes.maps.NotesEventMap;
			import com.asfusion.mate.utils.debug.Debugger;
			import org.bigbluebutton.common.mate.SharedObjectEvent;
			import mx.controls.Alert;
			import org.bigbluebutton.modules.notes.events.NotesUpdateEvent;
			
			private var _xPosition:int;
			private var _yPosition:int;
			
			private var timer:Timer;
			private var dirty:Boolean = true;
			
			//The following code block is to deal with a bug in FLexLib with MDI windows not responding well to being maximized
			private var savedWindowWidth:Number;
			private var savedWindowHeight:Number;
			private var savedX:Number;
			private var savedY:Number;
			private var isMaximized:Boolean = false;
			override public function maximize():void{
				if (!isMaximized){
					savedWindowHeight = this.height;
					savedWindowWidth = this.width;
					savedX = this.x;
					savedY = this.y;
                	dispatchEvent(new MDIWindowEvent(MDIWindowEvent.MAXIMIZE, this));
                	isMaximized = true;
				} else{
					this.width = savedWindowWidth;
					this.height = savedWindowHeight;
					this.x = savedX;
					this.y = savedY;
					isMaximized = false;
					//dispatchEvent(new MDIWindowEvent(MDIWindowEvent.RESTORE, this));
				}	
			}
			
			private function startTimer():void{
				timer = new Timer(1000);
				timer.addEventListener(TimerEvent.TIMER, timerHandler);
				timer.start();
			}
			
			private function timerHandler(e:TimerEvent):void{
				if (!dirty) return;
				var messageEvent:NotesUpdateEvent = new NotesUpdateEvent(NotesUpdateEvent.NEW);
				messageEvent.message = txtArea.text;
				dispatchEvent(messageEvent);
				dirty = false;
			}
			
			//Sends out an update to all the other clients
			public function sendNewMessage(message:String):void{
				dirty = true;
			}
			
			//Displays the updated message
			private function displayNewMessage(event:SharedObjectEvent):void{
				txtArea.text = event.message.toString();
			}
			
			public function get xPosition():int {
				return _xPosition;
			} 
			
			public function get yPosition():int {
				return _yPosition;
			}
			
			public function set xPosition(x:int):void {
				_xPosition = x;
			}
			
			public function set yPosition(y:int):void {
				_yPosition = y;
			}
			
			private var _defaultWidth:int = 400;
			private var _defaultHeight:int = 300;
			
			public function get defaultWidth():int{
				return _defaultWidth;
			}
			
			public function get defaultHeight():int{
				return _defaultHeight;
			}
			
			public function set defaultHeight(height:int):void{
				this._defaultHeight = height;
			}
			
			public function set defaultWidth(width:int):void{
				this._defaultWidth = width;
			}
			
			public function resetWidthAndHeight():void{
				this.width = _defaultWidth;
				this.height = _defaultHeight;
			}
			
		]]>
	</mx:Script>
	
	<!-- The text area where the notes get displayed -->
	<mx:TextArea id="txtArea" width="100%" height="100%" keyUp="sendNewMessage(txtArea.text)" />
	
	<!-- This listener calls the displayNewMessage method every time a NOTES_UPDATE_EVENT gets dispatched -->
	<mate:Listener type="{NotesEventMap.NOTES_UPDATE_EVENT}" method="displayNewMessage" />
</MDIWindow>
